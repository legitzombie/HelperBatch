@echo off

REM  ▄▄▄██▀▀▀▄▄▄       ███▄ ▄███▓▓█████   ██████
REM    ▒██  ▒████▄    ▓██▒▀█▀ ██▒▓█   ▀ ▒██    ▒
REM    ░██  ▒██  ▀█▄  ▓██    ▓██░▒███   ░ ▓██▄
REM ▓██▄██▓ ░██▄▄▄▄██ ▒██    ▒██ ▒▓█  ▄   ▒   ██▒
REM  ▓███▒   ▓█   ▓██▒▒██▒   ░██▒░▒████▒▒██████▒▒
REM  ▒▓▒▒░   ▒▒   ▓▒█░░ ▒░   ░  ░░░ ▒░ ░▒ ▒▓▒ ▒ ░
REM  ▒ ░▒░    ▒   ▒▒ ░░  ░      ░ ░ ░  ░░ ░▒  ░ ░
REM  ░ ░ ░    ░   ▒   ░      ░      ░   ░  ░  ░
REM  ░   ░        ░  ░       ░      ░  ░      ░
REM
REM          ▄▄▄█████▓ ██░ ██ ▓█████
REM          ▓  ██▒ ▓▒▓██░ ██▒▓█   ▀
REM          ▒ ▓██░ ▒░▒██▀▀██░▒███
REM          ░ ▓██▓ ░ ░▓█ ░██ ▒▓█  ▄
REM            ▒██▒ ░ ░▓█▒░██▓░▒████▒
REM            ▒ ░░    ▒ ░░▒░▒░░ ▒░ ░
REM              ░     ▒ ░▒░ ░ ░ ░  ░
REM            ░       ░  ░░ ░   ░
REM                    ░  ░  ░   ░  ░
REM
REM  ██░ ██  ▄▄▄       ▄████▄   ██ ▄█▀▓█████  ██▀███
REM ▓██░ ██▒▒████▄    ▒██▀ ▀█   ██▄█▒ ▓█   ▀ ▓██ ▒ ██▒
REM ▒██▀▀██░▒██  ▀█▄  ▒▓█    ▄ ▓███▄░ ▒███   ▓██ ░▄█ ▒
REM ░▓█ ░██ ░██▄▄▄▄██ ▒▓▓▄ ▄██▒▓██ █▄ ▒▓█  ▄ ▒██▀▀█▄
REM ░▓█▒░██▓ ▓█   ▓██▒▒ ▓███▀ ░▒██▒ █▄░▒████▒░██▓ ▒██▒
REM  ▒ ░░▒░▒ ▒▒   ▓▒█░░ ░▒ ▒  ░▒ ▒▒ ▓▒░░ ▒░ ░░ ▒▓ ░▒▓░
REM  ▒ ░▒░ ░  ▒   ▒▒ ░  ░  ▒   ░ ░▒ ▒░ ░ ░  ░  ░▒ ░ ▒░
REM  ░  ░░ ░  ░   ▒   ░        ░ ░░ ░    ░     ░░   ░
REM  ░  ░  ░      ░  ░░ ░      ░  ░      ░  ░   ░
REM  

setlocal enabledelayedexpansion

set "UTILS_DIR=Utils"
set "VERSION_FILE=%UTILS_DIR%\version.txt"
set "REMOTE_VERSION_URL=https://raw.githubusercontent.com/legitzombie/HelperBatch/main/Shareable/Utils/version.txt"
set "BASE_URL=https://raw.githubusercontent.com/legitzombie/HelperBatch/main/Shareable/Utils"
set FILES=CLEANUP.bat FINDGAME.bat GET7ZIP.bat GETATLASGENERATOR.bat GETBINK.bat GETCLI.bat GETDOTNET.bat GETPILLOW.bat GETPIP.bat GETPYTHON.bat GETREDIST.bat GETREQUESTS.bat MP4TOBK2.bat

if not exist "%UTILS_DIR%" mkdir "%UTILS_DIR%"

set "TEMP_VERSION_FILE=%TEMP%\utils_version.tmp"
curl -s -L -o "%TEMP_VERSION_FILE%" "%REMOTE_VERSION_URL%"
if errorlevel 1 (
    echo [WARN] Could not fetch remote version, update cancelled.
    goto :done
)
set /p REMOTE_VERSION=<"%TEMP_VERSION_FILE%"
del "%TEMP_VERSION_FILE%"

set "LOCAL_VERSION="
if exist "%VERSION_FILE%" (
    set /p LOCAL_VERSION=<"%VERSION_FILE%"
)

if "%REMOTE_VERSION%"=="%LOCAL_VERSION%" (
    goto :done
)

echo [INFO] Updating helper files to version %REMOTE_VERSION%...
for %%F in (%FILES%) do (
    echo Downloading %%F...
    curl -s -L -o "%UTILS_DIR%\%%F" "%BASE_URL%/%%F"
)

echo %REMOTE_VERSION%>"%VERSION_FILE%"
echo [INFO] Helpers updated to version %REMOTE_VERSION%.

:done

exit /b 0

